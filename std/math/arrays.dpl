#namespace arrays
#include <../exception.dpl>
#include <exp.dpl>
#include <../mem>

fn max:<T>(arr: array[T]): T {
	// Return the maximum value in the array
	if (arr.len == 0) {
		exception("Array is empty", 1);
	}
	let max: T = arr[0];
	for (let i: int = 1; i < arr.len; i = i + 1) {
		if (arr[i] > max) {
			max = arr[i];
		}
	}
	return max;
}

fn min:<T>(arr: array[T]): T {
	// Return the minimum value in the array
	if (arr.len == 0) {
		exception("Array is empty", 1);
	}
	let min: T = arr[0];
	for (let i: int = 1; i < arr.len; i = i + 1) {
		if (arr[i] < min) {
			min = arr[i];
		}
	}
	return min;
}

fn sum:<T>(arr: array[T]): T {
	// Return the sum of all elements in the array
	let sum: T;
	for (let i: int = 0; i < arr.len; i = i + 1) {
		sum = sum + arr[i];
	}
	return sum;
}

fn softmax(arr: array[float]): array[float] {
	// Compute the softmax of the input array. Not in-place
	let max_val: float = max:<float>(arr);
	let exps: array[float] = mem::malloc(arr.len): array[float];
	for (let i: int = 0; i < arr.len; i = i + 1) {
		exps[i] = exp(arr[i] - max_val);
	}
	let sum_exps: float = sum:<float>(exps);
	for (let i: int = 0; i < exps.len; i = i + 1) {
		exps[i] = exps[i] / sum_exps;
	}
	return exps;
}